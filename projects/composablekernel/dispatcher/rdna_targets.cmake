# Copyright (c) Advanced Micro Devices, Inc., or its affiliates.
# SPDX-License-Identifier: MIT
#
# Shared helper for the grouped-convolution 'rdna' production catalog.
#
# Included from sibling directories, so it deliberately has no include_guard():
# every including scope needs its own copy of the function below.

# Set <out_var> to the first supported RDNA target present in <targets>, or "".
# Iteration is over the candidate list, so the result does not depend on how the
# caller ordered GPU_TARGETS. The candidate list is only a superset filter: the
# generator re-derives datatype support from arch_specs and aborts configuration
# with a fatal error if a listed target is not actually supported there.
function(ck_dispatcher_rdna_target targets out_var)
    set(_candidates gfx1100 gfx1200 gfx1201)
    string(REPLACE " " ";" _requested "${targets}")
    list(FILTER _requested EXCLUDE REGEX "^$")
    foreach(_candidate IN LISTS _candidates)
        list(FIND _requested "${_candidate}" _index)
        if(NOT _index EQUAL -1)
            set(${out_var} "${_candidate}" PARENT_SCOPE)
            return()
        endif()
    endforeach()
    set(${out_var} "" PARENT_SCOPE)
endfunction()
